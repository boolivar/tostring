plugins {
    id "java"
    id "jacoco"
    id "pmd"
    id "checkstyle"
    id "com.github.spotbugs"
}

group = "org.bool"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.release = 8
}

tasks.withType(Javadoc).configureEach {
    options.addBooleanOption("Xdoclint:none", true)
}

tasks.named("test") {
    useJUnitPlatform()
    jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
    finalizedBy "jacocoTestReport"
}

tasks.named("jacocoTestReport") {
    reports {
        xml.required = true
    }
}

tasks.named("jacocoTestCoverageVerification") {
    dependsOn "test"
    violationRules {
        rule {
            limit {
                minimum = jacocoMinRatio as BigDecimal
            }
        }
    }
}

tasks.named("check") {
    dependsOn "jacocoTestCoverageVerification"
}

tasks.named("jar") {
    dependsOn "check"
}

checkstyle {
    configDirectory = file("$rootDir/gradle/config/checkstyle")
    toolVersion = com.puppycrawl.tools.checkstyle.Checker.package.implementationVersion
}

pmd {
    consoleOutput = true
    ruleSets = ["ruleset.xml"]
    toolVersion = net.sourceforge.pmd.PMDVersion.VERSION
}

pmdTest {
    ruleSets = ["test-ruleset.xml"]
}

tasks.withType(Pmd).configureEach {
    pmdClasspath += files("$rootDir/gradle/config/pmd")
}

spotbugs {
    omitVisitors = ["FindReturnRef"]
}

repositories {
    mavenCentral()
}

configurations {
    mockitoAgent
}

dependencies {
    implementation platform(project(":platform"))
    mockitoAgent platform(project(":platform"))

    testImplementation "org.assertj:assertj-core"
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.mockito:mockito-junit-jupiter"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    mockitoAgent("org.mockito:mockito-core") {
        transitive = false
    }
}
